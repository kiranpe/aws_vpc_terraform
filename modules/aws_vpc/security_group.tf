//SECURITY GROUP
#################

locals {
  secgrp_tags = {
    createdby   = "Kiran Peddineni"
    App         = "${terraform.workspace}_vpc_security_group"
    Approver    = "Kiran Peddineni"
    description = "Prod VPC Security Group"
  }
}

resource "aws_default_security_group" "security_group" {
  count = var.create_vpc ? 1 : 0

  vpc_id = aws_vpc.vpc[0].id

  dynamic "ingress" {
    for_each = var.default_security_group_ingress
    content {
      self             = lookup(ingress.value, "self", null)
      cidr_blocks      = compact(split(",", lookup(ingress.value, "cidr_blocks", "")))
      security_groups  = compact(split(",", lookup(ingress.value, "security_groups", "")))
      description      = lookup(ingress.value, "description", null)
      from_port        = lookup(ingress.value, "from_port", 0)
      to_port          = lookup(ingress.value, "to_port", 0)
      protocol         = lookup(ingress.value, "protocol", "-1")
    }
  }

  dynamic "egress" {
    for_each = var.default_security_group_egress
    content {
      self             = lookup(egress.value, "self", null)
      cidr_blocks      = compact(split(",", lookup(egress.value, "cidr_blocks", "")))
      security_groups  = compact(split(",", lookup(egress.value, "security_groups", "")))
      description      = lookup(egress.value, "description", null)
      from_port        = lookup(egress.value, "from_port", 0)
      to_port          = lookup(egress.value, "to_port", 0)
      protocol         = lookup(egress.value, "protocol", "-1")
    }
  }

  tags = merge(
    {
      "Name" = "${var.sec_grp_name}-${terraform.workspace}"
    },
    local.secgrp_tags
  )
}
